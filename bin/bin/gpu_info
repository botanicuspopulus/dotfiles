#!/bin/zsh

script_dir=${0:A:h}

# Determine the gpus in the system
# Check if there are any nvidia gpus

GREEN='#9ECE6A'
YELLOW='#E0AF86'
RED='#f7768E'
WHITE='#C0CAF5'

function color() {
    temp=$1
    if (( $temp < 50 )); then
      echo -e  "<span color=\\\"${GREEN}\\\">$temp</span>"
    elif (( $temp < 70 )); then
      echo -e "<span color=\\\"${YELLOW}\\\">$temp</span>"
    else
      echo -e "<span color=\\\"${RED}\\\">$temp</span>"
    fi
}

vga_devices=$(lspci | grep -i vga)
amd_gpus=$(grep -ic amd <<< "$vga_devices")
nvidia_gpus=$(grep -ic nvidia <<< "$vga_devices")

output_text=''

if (( $nvidia_gpus == 0 )) && (( $amd_gpus == 0 )) && (( $intel_gpus == 0 )); then
    echo 'No GPUs found'
    exit 0
fi

if (( $nvidia_gpus > 0 )); then
    # Get the temperature of the nvidia gpus
    fields=(
      name,
      temperature.gpu
      utilization.gpu
      temperature.memory
      utilization.memory
      memory.total
      memory.free
      memory.used
      power.draw
      clocks.gr
      clocks.mem
    )
    
    field_names=(
      "Name"
      "Core Temp"
      "Core Utilization"
      "Mem Temp"
      "Mem Utilization"
      "Mem Total"
      "Mem Free"
      "Mem Used"
      "Power Draw"
      "Graphics Clock"
      "Memory Clock"
    )

    max_width=0
    for field in "${field_names[@]}"; do
        if (( ${#field} > max_width )); then
            max_width=${#field}
        fi
    done
    max_width=$((max_width + 2))
    icon_width=3

    for i in {1..11}; do
      field=${field_names[$i]}
      case $field in
        *Temp)
          icon="󰔏"
          ;;
        *Utilization)
          icon=""
          ;;
        Power*)
          icon="󱐌"
          ;;
        *Clock)
          icon="󰅐"
          ;;
        "Mem Total" | "Mem Free" | "Mem Used")
          icon="󰘚"
          ;;
      esac

      field_names[$i]=$(printf "%-${icon_width}s%-${max_width}s" "${icon}" "${field}")
    done

    gpu_info=$(nvidia-smi --query-gpu="${(j:,:)fields}" --format=csv,noheader)
    output_text+="<u>󰢮  NVIDIA GPU</u>\n"
    while IFS= read -r line; do
        IFS=',' read -r -A gpu_info_array <<< "$line"
        for i in {1..11}; do
            info=${gpu_info_array[$i]}
            field=${field_names[$i]}
            if [[ ${info} =~ "N/A" ]]; then
                continue
            fi

            if [[ $field =~ ".* Temp" ]]; then
              info="$(color $info)°C"
            fi

            if [[ $field =~ ".* Utilization" ]]; then
              info="$(color ${info%?})%"
            fi
            
            output_text+="${field}: ${info}\n"
        done
    done <<< "$gpu_info"
fi

if (( $amd_gpus > 0 )); then
    # Get the temperature of the amd gpus
    amd_temp=$(sensors | grep -i amdgpu | awk '{print $2}' | cut -c 2-3)
    output_text+="AMD GPU\nTemperature: $amd_temp\n\n"
fi

# return the output as json
printf '{ "text": "󰢮  GPU", "tooltip": "%s" }' "$output_text"
